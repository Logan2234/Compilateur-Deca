// little typing game 

#include "io.deca"
#include "time.deca"
#include "random.deca"

class Game {
    LinkedChars ESCAPECHAR = new LinkedChars();
    protected IO io = new IO();
    protected Timer timer = new Timer();
    protected Random random = new Random();

    Timer getTimer(){
        return timer;
    }

    IO getIO(){
        return io;
    }

    Random getRandom(){
        return random;
    }

    void clearScreen(){
        int i = 0;
        while (i < 20) {
            println("");
            i = i + 1;
        }
    }

    void printDelayedNum(int num){
        print(num);
        timer.wait(0.25);
        print(".");
        timer.wait(0.25);
        print(".");
        timer.wait(0.25);
        print(". ");
        timer.wait(0.25);
    }

    void gameLoop(){
        // Decl vars
        boolean play = true;
        LinkedChars currentChar;
        LinkedChars userInput; 
        String str;
        int taille;
        int score = 0;
        int offset = 0;
        boolean wonWord;
        float time = 0;
        float gameDuration = 30;
        
        // Counter 3 2 1 GO
        clearScreen();
        print("Game will start in... ");
        printDelayedNum(3);
        printDelayedNum(2);
        printDelayedNum(1);
        print("GOOOO!");
        println("");
        println("");

        // Setup the timer
        timer.reset();
        while (play && timer.timeSec() < gameDuration){ // loop over words
            str = random.generateRandomString(random, 5 + offset, 8 + offset);
            taille = str.getSize();
            wonWord = true;
            
            println("");
            str.display(io);
            
            while (str.getSize() > 0) { // loop over letters
                currentChar = str.getFirst();
                userInput = io.readChar();
                if (userInput.equals(ESCAPECHAR)) {
                    play = false;
                    str = new String();
                } else {
                    if (!currentChar.equals(userInput)) {
                        print("/");
                        str.pop();
                        while(str.getSize() > 0) {
                            print(" -");
                            str.pop();
                        }
                        wonWord = false;
                    }
                    else {
                        io.printChar(userInput.getValue());
                        print(" ");
                        str.pop();
                    }
                }
            }

            if (wonWord && timer.timeSec() < gameDuration){
                print("  => + ", taille, " time left : ", gameDuration - timer.timeSec());
                score = score + taille;
                offset = offset + 1;
            }
            println("");
        }
        println("");
        println("Finished ! Score: ", score);
        home();
    }

    void home(){
        int choice;
        println("");
        println("");
        println(" ===== Menu =====");
        println("");
        println("1. How to play");
        println("2. Play");
        println("3. Quit");
        println("");
        print("Choice: ");
        choice = readInt();
        println("");
        println("");

        if (choice == 2) {
            gameLoop();
        } else if (choice == 1) {
            howToPlay();
            home();
        } else {
            println("Bye!");
        }
        
    }
    
    void howToPlay() {
        clearScreen();
        println(" ===== How to play =====");
        println("");
        println("Sequence of letters will appear.");
        println("You will have 30 seconds to type as much sequences as possible.");
        println("Each completed sequence will give you points accordingly to the sequence size.");
        println("But if you enter one wrong character, you will gain no points and have to go to the next sequence.");
        println("Have fun !");
        println("");
    }

    void start(){
        // init values
        ESCAPECHAR.setChar(27);

        // cool display °3°
        print("Welcome to");
        print(" W");
        timer.waitASemiSemiSec();
        print("o");
        timer.waitASemiSemiSec();
        print("r");
        timer.waitASemiSemiSec();
        print("d");
        timer.waitASemiSemiSec();
        print(" T");
        timer.waitASemiSemiSec();
        print("y");
        timer.waitASemiSemiSec();
        print("p");
        timer.waitASemiSemiSec();
        print("e");
        timer.waitASemiSemiSec();
        println("r!");
        home();
    }
}



{
    Game game = new Game();
    game.start();
}

